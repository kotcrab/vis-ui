apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    def generateJavadoc = project.hasProperty("generateJavadoc")
    onlyIf { (isReleaseVersion || gradle.taskGraph.hasTask(":ui:publishToSonatype")) || generateJavadoc }

    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                url = 'https://github.com/kotcrab/vis-ui/'

                scm {
                    connection = 'scm:git:git@github.com:kotcrab/vis-ui.git'
                    developerConnection = 'scm:git:git@github.com:kotcrab/vis-ui.git'
                    url = 'git@github.com:kotcrab/vis-ui.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'kotcrab'
                        name = 'Kotcrab'
                        url = 'https://kotcrab.com'
                    }
                }

            }
        }
    }
}

signing {
    required { isReleaseVersion || gradle.taskGraph.hasTask(":ui:publishToSonatype") }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}

task publishSnapshot {
    if (!isReleaseVersion) {
        finalizedBy(tasks["publishToSonatype"])
    }
}
