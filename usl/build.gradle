group = 'com.kotcrab.vis'
version = '0.2.2-SNAPSHOT'

apply plugin: "java"
apply plugin: 'maven'
apply from: '../deploy.gradle'

archivesBaseName = "vis-usl"
sourceCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    testCompile "junit:junit:$junitVersion"
}

eclipse.project {
    name = appName + "-usl"
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name 'vis-usl'
                artifactId 'vis-usl'
                packaging 'jar'
                description 'UI styling language for scene2d.ui'
            }
        }
    }
}

task run(type: JavaExec) {
    main = 'com.kotcrab.vis.usl.Main'
    classpath = sourceSets.main.runtimeClasspath
    ignoreExitValue = true
    if (project.hasProperty("runArgs")) {
        args = Eval.me(runArgs)
    }
}
