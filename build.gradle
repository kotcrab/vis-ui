buildscript {
    ext.kotlinVersion = '1.0.3'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath "com.kotcrab.vis:vis-usl:0.2.1"
        classpath "com.badlogicgames.gdx:gdx-tools:1.9.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'distribution'
apply plugin: 'io.codearte.nexus-staging'

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "nebula.provided-base"
    apply plugin: "nebula.optional-base"

    version = '0.4.0-SNAPSHOT'
    ext {
        // Build Properties
        appName = 'vis'
        mainClassName = 'com.kotcrab.vis.editor.Main'
        // Dependency Properties
        gdxVersion = '1.9.5'
        editorGdxVersion = '1.9.5'
        box2dLightsVersion = '1.4'
        jnaVersion = '4.1.0'
        jnaPlatformVersion = '3.5.2'
        appleJavaExtensionsVersion = '1.4'
        junitVersion = '4.12'
        artemisVersion = '2.0.0'
        kryoVersion = '3.0.1'
        commonsIoVersion = '2.4'
        commonsLangVersion = '3.3.2'
        commonsExecVersion = '1.3'
        guavaVersion = '18.0'
        jsoupVersion = '1.8.2'
        slf4jVersion = '1.7.12'
        imgscalrVersion = '4.2'
        gsonVersion = '2.4'
        cloningVersion = '1.9.2'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(':editor') {
    apply plugin: "java"

    dependencies {
        compile project(":runtime")
        compile project(":ui")
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        compile "com.badlogicgames.gdx:gdx:$editorGdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$editorGdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$editorGdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype:$editorGdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$editorGdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d:$editorGdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$editorGdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$editorGdxVersion"
        compile "com.badlogicgames.box2dlights:box2dlights:$box2dLightsVersion"
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
        compile "com.esotericsoftware:kryo:$kryoVersion"
        compile "commons-io:commons-io:$commonsIoVersion"
        compile "org.apache.commons:commons-lang3:$commonsLangVersion"
        compile "org.apache.commons:commons-exec:$commonsExecVersion"
        compile "net.java.dev.jna:jna:$jnaVersion"
        compile "net.java.dev.jna:platform:$jnaPlatformVersion"
        compile "com.google.guava:guava:$guavaVersion"
        compile "org.jsoup:jsoup:$jsoupVersion"
        compile "org.slf4j:slf4j-simple:$slf4jVersion"
        compile "org.imgscalr:imgscalr-lib:$imgscalrVersion"
        compile "com.google.code.gson:gson:$gsonVersion"
        compile "uk.com.robust-it:cloning:$cloningVersion"
        provided "com.apple:AppleJavaExtensions:$appleJavaExtensionsVersion", optional

        testCompile project(":plugins:vis-runtime-spine")
        testCompile project(":plugins:vis-runtime-spriter")
    }
}

project(':runtime') {
    apply plugin: "java"

    dependencies {
        provided "com.badlogicgames.gdx:gdx:$gdxVersion"
        provided "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        provided "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        provided "com.badlogicgames.box2dlights:box2dlights:$box2dLightsVersion"
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
    }
}

project(':ui') {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        provided "com.apple:AppleJavaExtensions:$appleJavaExtensionsVersion", optional
        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testCompile "junit:junit:$junitVersion"
        //testCompile "org.imgscalr:imgscalr-lib:4.2"
    }
}

project(':plugins:vis-editor-spine') {
    apply plugin: 'java'

    dependencies {
        provided project(':editor')
        provided project(':plugins:vis-runtime-spine')
    }
}

project(':plugins:vis-runtime-spine') {
    apply plugin: 'java'

    dependencies {
        provided project(':runtime')
        provided "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project(':plugins:vis-editor-spine-notifier') {
    apply plugin: 'java'

    dependencies {
        provided project(':editor')
        provided "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project(':plugins:vis-editor-spriter') {
    apply plugin: 'java'

    dependencies {
        provided project(':editor')
        provided project(':plugins:vis-runtime-spriter')
    }
}

project(':plugins:vis-runtime-spriter') {
    apply plugin: 'java'

    dependencies {
        provided project(':runtime')
        provided "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project(':usl') {
    apply plugin: 'java'

    dependencies {
        testCompile "junit:junit:$junitVersion"
    }
}

project(':tools:crash-reporter') {
    apply plugin: 'java'

    dependencies {
        compile "org.apache.commons:commons-exec:$commonsExecVersion"
        testCompile "junit:junit:$junitVersion"
    }
}

nexusStaging {
    packageGroup = "com.kotcrab"
    username = ossrhUsername
    password = ossrhPassword
}

tasks.eclipse.doLast {
    delete ".project"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task uploadRuntimeAndPluginArchives(dependsOn: [':runtime:uploadArchives', ':plugins:vis-runtime-spriter:uploadArchives']) {
    description 'Upload runtime and plugin runtimes archives'
}

distributions {
    main {
        baseName = "VisEditor"
        contents {
            project(':editor') {
                from jar
                into('lib') {
                    from configurations.runtime
                }
            }

            def plugin = project(':plugins:vis-editor-spine')
            into('plugins/spine') {
                from plugin.jar
            }
            plugin = project(':plugins:vis-runtime-spine')
            into('plugins/spine/lib') {
                from plugin.jar
            }
            plugin = project(':plugins:vis-editor-spine-notifier')
            into('plugins/spine-notifier') {
                from plugin.jar
            }

            plugin = project(':plugins:vis-editor-spriter')
            into('plugins/spriter') {
                from plugin.jar
            }
            plugin = project(':plugins:vis-runtime-spriter')
            into('plugins/spriter/lib') {
                from plugin.jar
            }

            def tool = project(':tools:crash-reporter')
            into('tools') {
                from tool.jar
                rename("(.*).jar", "crash-reporter.jar")
            }
        }
    }
}
